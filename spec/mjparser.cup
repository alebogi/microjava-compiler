

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROG, L_BRACE, R_BRACE, CONST, EQUAL, SEMI, COMMA, CLASS, EXTENDS, VOID;
terminal R_PAREN, L_PAREN, L_SQUARE_BRACKET, R_SQUARE_BRACKET, INCREMENT, DECREMENT;
terminal EQUALS_TO, NOT_EQUAL, GREATER, GREATER_OR_EQUAL, LESS, LESS_OR_EQUAL, PLUS, MINUS, MULTIPLY, DIVIDE, MOD;
terminal LOGICAL_OR, LOGICAL_AND, NEW, DOT, CONTINUE, BREAK, DO, WHILE, RETURN, READ, PRINT, IF, ELSE;

terminal Integer NUM_CONST;
terminal String IDENT;
terminal Character CHAR_CONST;
terminal Boolean BOOL_CONST;

nonterminal Program, DeclsList, MethodDeclList, Decls, ConstDecl, VarDecl, ClassDecl, Consts, ConstList, ConstListEnd, Type, Arr, VarDeclList;
nonterminal VarDeclListEnd, ExtendsClass, ListOfVarDecl, MethodType, ListOfStmt, FormPars, Statement, MethodDecl;
nonterminal FormParsList, FormParsListEnd, ElseExists, DesignatorStatement, Designator, Condition, Expr, ExprList, ExprListEnd;
nonterminal CondTerm, CondTermOrList, CondFact, CondFactAndList, RelopExprExists, ActPars, ActParsExists, DesgStmtEnd, StmtList, ExprExists;
nonterminal Assignop, Relop, Addop, Mulop, AddopTermList, MinusExists, Term, ExprArrExists, MulopFactorList, Factor, DesgnListEnd, DesgnList;

Program ::= (Program) PROG IDENT:progName DeclsList L_BRACE MethodDeclList R_BRACE
;

DeclsList ::= (DeclarationsList) DeclsList Decls
			|			
			(NoDeclarationsList) /* epsilon */
;

Decls ::= (DeclsConst) ConstDecl 
		|
		(DeclsVar) VarDecl
		|
		(DeclsClass) ClassDecl
;
		

ConstDecl ::= (ConstDeclaration) CONST Type:constType IDENT:constName EQUAL Consts ConstList SEMI
;

ConstList ::= (ConstantsList) ConstList ConstListEnd
			|
			(NoConstantsList) /* epsilon */
;

ConstListEnd ::= (ConstantsListEnd) COMMA IDENT:constEndName EQUAL Consts
;

Consts ::= (NumConst) NUM_CONST:numValue
			|
			(CharConst) CHAR_CONST:charValue
			|
			(BoolConst) BOOL_CONST:boolValue
;

VarDecl ::= (VarDeclaration) Type:varType IDENT:varName Arr VarDeclList
;

Arr ::= (Array) L_SQUARE_BRACKET R_SQUARE_BRACKET
		|
		(NoArray) /* epsilon */
;

VarDeclList ::= (VarDeclarationList) VarDeclList VarDeclListEnd
				|
				(NoVarDeclarationList) /* epsilon */
;

VarDeclListEnd ::= (VarDeclarationListEnd) COMMA IDENT:varDeclListEndName Arr SEMI
;

ClassDecl ::= (ClassDeclarationMethods) CLASS IDENT:className ExtendsClass L_BRACE ListOfVarDecl L_BRACE MethodDeclList R_BRACE R_BRACE
				|
				(ClassDeclaration) CLASS IDENT:className ExtendsClass L_BRACE ListOfVarDecl  R_BRACE
;

ExtendsClass ::= (ClassExtends) EXTENDS Type:extendsType
				|
				(NoClassExtends) /* epsilon */
;


ListOfVarDecl ::= (ListVarDeclaration) ListOfVarDecl VarDecl
				|
				(NoListOfVarDeclaration) /* epsilon */
;


MethodDeclList ::= (MethodsDeclarationList) MethodDeclList MethodDecl
					|
					(NoMethodsDeclarationList) /* epsilon */
;

MethodDecl ::= (MethodDeclarationFormPars) MethodType IDENT:methodName L_PAREN FormPars R_PAREN ListOfVarDecl L_BRACE ListOfStmt R_BRACE
			|
			(MethodDeclaration) MethodType IDENT:methodName L_PAREN R_PAREN ListOfVarDecl L_BRACE ListOfStmt R_BRACE
; 

MethodType ::= (MethodWithType) Type:methodType
			|
			(MethodVoid) VOID
;
			

ListOfStmt ::= (ListOfSatemant) ListOfStmt Statement
			|
			(NoListOfSatemant) /* epsilon */
;
			
FormPars ::= (FormParams) Type:formParsType IDENT:formParsName Arr FormParsList
;

FormParsList ::= (FormParamsList) FormParsList FormParsListEnd
			|	
			(NoFormParsList) /* epsilon */
;			
			
FormParsListEnd ::= (FormParamsListEnd)	COMMA Type:fpListType IDENT:fpListName Arr
;	
			
Type ::= (Type) IDENT:typeName
;

Statement ::= (StmtDesgStmt) DesignatorStatement SEMI
			|	
			(StmtIfElse) IF L_PAREN Condition R_PAREN Statement ElseExists
			|
			(StmtRead) READ L_PAREN Designator R_PAREN SEMI
			|
			(StmtPrintNumConst) PRINT L_PAREN Expr COMMA NUM_CONST:numVal R_PAREN SEMI
			|
			(StmtPrint) PRINT L_PAREN Expr R_PAREN SEMI
			|
			(StmtDoWhile) DO Statement WHILE L_PAREN Condition R_PAREN SEMI
			|
			(StmtBreak) BREAK SEMI
			|
			(StmtCont) CONTINUE SEMI
			|
			(StmtRet) RETURN ExprExists SEMI
			|
			(StmtStmt) L_BRACE StmtList R_BRACE 
;
			
			
ElseExists ::= (ElseBranch) ELSE Statement
			|
			(NoElse) /* epsilon */
;			
			
			
	
				
ExprExists ::= (ExpreExists) Expr
				|
				(NoExprExists) /* epsilon */
;		
				
StmtList ::= (StatementList) StmtList Statement
			|
			(NoStatementList) /* epsilon */
;
			
DesignatorStatement ::=	(DesignStmt) Designator DesgStmtEnd
;

DesgStmtEnd ::= (DesgnStmtAsgnOp) Assignop Expr
				|
				(DesgnStmtActPars)  ActParsExists 	
				|
				(DesgnStmtIncr) INCREMENT
				|
				(DesgnStmtDecr) DECREMENT
;
				
ActParsExists ::= (ActParamsExists) L_PAREN ActPars R_PAREN
					|
					(NoActParamsExists) /* epsilon */
;
			
			
ActPars ::= (ActParams) Expr ExprList
;

ExprList ::= (ExpListComma) ExprList ExprListEnd
			|			
			(NoExpListComma) /* epsilon */
;

ExprListEnd ::= (ExpListEnd) COMMA Expr
;

Condition ::= (Cond) CondTerm CondTermOrList
;			
			
CondTermOrList ::= (CondTermListOr) CondTermOrList LOGICAL_OR CondTerm	
				|
				(NoCondTermLisOr) /* epsilon */
;

CondTerm ::= (ConditionTerm) CondFact CondFactAndList
;

CondFactAndList ::= (CondFactListAnd) CondFactAndList LOGICAL_AND CondFact
					|
					(NoCondFactListAnd) /* epsilon */
;

CondFact ::= (ConditionFact) Expr RelopExprExists
;

RelopExprExists ::= (RelopExprExist) Relop Expr
				|
				(NoRelopExprExist) /* epsilon */
;
	
Expr ::= (Expression) MinusExists Term AddopTermList
;

MinusExists ::= (MinusExs) MINUS
				|
				(NoMinusExs) /* epsilon */
;	
				
AddopTermList ::= (ListAddopTerm) AddopTermList Addop Term
				|
				(NoListAddopTerm) /* epsilon */
;
				
Term ::= (Term) Factor MulopFactorList
;

MulopFactorList ::= (MullopFactList) MulopFactorList Mulop Factor
				|
				(NoMullopFactList) /* epsilon */
;
				
Factor ::= (FactorDesign) Designator ActParsExists
		|
		(FactorNumConst) NUM_CONST
		|
		(FactorCharConst) CHAR_CONST
		|
		(FactorBoolConst) BOOL_CONST
		|
		(FactorNew) NEW Type ExprArrExists
		|
		(FactorExpr) L_PAREN Expr R_PAREN
;

ExprArrExists ::= (ArrExprExists) L_SQUARE_BRACKET Expr R_SQUARE_BRACKET
				|
				(NoArrExprExists) /* epsilon */
;

Designator ::= (Design) IDENT:DesgnName DesgnList
;

DesgnList ::= (DesignList) DesgnList DesgnListEnd
			|
			(NoDesignList) /* epsilon */
;
			
DesgnListEnd ::= (DesignListEnd) DOT IDENT:DesgnNameId
				|
				(DesgnListEndExpr) L_SQUARE_BRACKET Expr R_SQUARE_BRACKET
;	
	
Assignop ::= (AssignOperator) EQUAL
;

Relop ::= (RelopE) EQUALS_TO
		|
		(RelopNE) NOT_EQUAL
		|
		(RelopG) GREATER
		|
		(RelopGE) GREATER_OR_EQUAL
		|
		(RelopL) LESS
		|
		(RelopLE) LESS_OR_EQUAL
;		
			
Addop ::= (OpAdd) PLUS
		|
		(SubOp) MINUS
;
		
Mulop ::= (OpMul) MULTIPLY
		|
		(DivOp) DIVIDE
		|
		(ModOp) MOD
;
